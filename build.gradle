buildscript {
    ext.with {
        hasGbArtifactory = (project.hasProperty('gbArtifactoryUrl')
            && project.hasProperty('gbArtifactoryUser')
            && project.hasProperty('gbArtifactoryPassword'))
        
        hasGbUploadArtifactory = (project.hasProperty('gbArtifactoryUploadUrl')
            && project.hasProperty('gbArtifactoryUser')
            && project.hasProperty('gbArtifactoryPassword'))
        
        setRepositoriesFor = { RepositoryHandler repositoryHandler ->
            if (hasGbArtifactory) {
                repositoryHandler.maven {
                    credentials {
                        username gbArtifactoryUser
                        password gbArtifactoryPassword
                    }
                    url gbArtifactoryUrl
                }
            } else {
                repositoryHandler.jcenter()
            }
            repositoryHandler.maven { url 'https://jitpack.io' }
        }
        batmDependencySubstitutionConfig = file('dependencySubstitutions.txt')
        batmDependencyChecksumsConfig = file('dependencyChecksums.txt')
    }
    setRepositoriesFor(repositories)
}

allprojects {
    setRepositoriesFor(buildscript.repositories)
    setRepositoriesFor(repositories)
    repositories {
        //bitcoin-json-rpc-client-1.0.jar isn't part of any well known maven repo
        //so we search the libs dir; gradle generates (guesses) metadata except dependencies.
        //Artifacts from repos with real metadata take precedence.
        flatDir {
            dirs rootProject.file('libs')
        }
    }
    tasks.withType(JavaCompile) {
        project.sourceCompatibility = JavaVersion.VERSION_1_8
        options.encoding = 'UTF-8'
        options.incremental = true
    }
}

allprojects {
    afterEvaluate {
        if (pluginManager.hasPlugin('com.generalbytes.gradle.dependency.verification')) {
            dependencyChecksums {
                global = true
            }
            dependencyVerifications {
//                setConfigurations([]) // effectively turns off all checksum verifications
//                failOnChecksumError = false // turns off failure on checksum problem (only warnings are printed)
            }
        }
        if (pluginManager.hasPlugin('com.generalbytes.gradle.dependency.strict')) {
            strictDependencies {
//                conflictFail false // turns off failure on version conflict
            }
        }
    }
}