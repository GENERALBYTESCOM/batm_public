// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
    }
}

final File repoDir = project.file('libs')
allprojects {
    repositories {
        jcenter()
    }
    repositories {
        //bitcoin-json-rpc-client-1.0.jar isn't part of any well known maven repo
        //so we search the libs dir; gradle generates (guesses) metadata except dependencies.
        //Artifacts from repos with real metadata take precedence.
        flatDir {
            dirs repoDir
        }
    }
    //cross-compilation stuff for Java 7
    assert (System.env.JDK_7_HOME != null): "Set environment property JDK_7_HOME"
    project.ext.targetJavaHome = System.env.JDK_7_HOME
    def javaExecutablesPath = new File(targetJavaHome, 'bin')
    def javaExecutables = [:].withDefault { execName ->
        def executable = new File(javaExecutablesPath, execName)
        assert executable.exists(): "There is no ${execName} executable in ${javaExecutablesPath}"
        executable
    }
    tasks.withType(JavaCompile) {
        project.sourceCompatibility = '1.7'
        options.with {
            fork = true
            forkOptions.javaHome = file(targetJavaHome)
        }
    }
    tasks.withType(Javadoc) {
        executable = javaExecutables.javadoc
    }
    tasks.withType(Test) {
        executable = javaExecutables.java
    }
    tasks.withType(JavaExec) {
        executable = javaExecutables.java
    }
}

ext.with {
    BatmDependencySubstitutionConfig = file('dependencySubstitutions.txt')
}