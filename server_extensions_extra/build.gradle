apply plugin: "java"
apply plugin: "distribution"
apply plugin: "com.generalbytes.gradle.main"
apply plugin: 'maven-publish'

group = projectGroup
version = projectVersion

jar {
    baseName 'batm_server_extensions_extra'
    archiveName "${baseName}.${extension}"
}

configurations {
    artifactOnly
    runtimeNoArtifact {
        extendsFrom compile
    }
}

artifacts {
    artifactOnly jar
}

dependencies {
    compile project(':server_extensions_api')
    compile(group: 'com.azazar', name: 'bitcoin-json-rpc-client', version: '1.1')
    compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25')
    compile(group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25')
    compile(group: 'com.github.mmazi', name: 'rescu', version: '1.9.1')
    compile(group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.0.1')
    compile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.11')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.11.2')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.11')
    compile(group: 'org.knowm.xchange', name: 'xchange-core', version: '4.2.3')
    compile(group: 'com.google.guava', name: 'guava', version: '18.0')
    compile(group: 'com.google.zxing', name: 'core', version: '2.3.0')
    compile(group: 'org.knowm.xchange', name: 'xchange-bitfinex', version: '4.2.3')
    compile(group: 'org.knowm.xchange', name: 'xchange-itbit', version: '4.2.3')
    compile(group: 'org.knowm.xchange', name: 'xchange-bittrex', version: '4.2.3')
    compile(group: 'org.knowm.xchange', name: 'xchange-hitbtc', version: '4.2.3')
    compile(group: 'org.web3j', name: 'core', version: '3.6.0')
    compile(group: 'org.web3j', name: 'crypto', version: '3.6.0')
    compile(group: 'org.web3j', name: 'utils', version: '3.6.0')
    compile(group: 'org.web3j', name: 'abi', version: '3.6.0')
    compile(group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.8.1')
    compile(group: 'io.reactivex', name: 'rxjava', version: '1.2.4')
    compile(group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.14.1') //bitcoinj

    runtime(group: 'com.madgag.spongycastle', name: 'core', version: '1.51.0.0')
    runtime(group: 'com.google.protobuf',  name: 'protobuf-java', version: '2.6.1')

    if (hasGbArtifactory) {
        compile(group: 'com.generalbytes.bitrafael.public', name: 'bitrafael-client', version: bitrafaelVersion)
    } else {
        compile(group: 'com.github.GENERALBYTESCOM.bitrafael_public', name: 'bitrafael-client', version: bitrafaelVersion)
    }

    testCompile (group: 'junit', name: 'junit', version: '4.10')
}

dependencySubstitutions {
    substitute file(batmDependencySubstitutionConfig)
}

distributions {
    main {
        contents {
            from jar
            from configurations.runtime
        }
    }
}

if (hasProperty('gbArtifactoryUser') && hasProperty('gbArtifactoryPassword') && hasProperty('gbArtifactoryUploadUrl')) {
    publishing {
        repositories {
            maven {
                credentials {
                    username gbArtifactoryUser
                    password gbArtifactoryPassword
                }
                url gbArtifactoryUploadUrl
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

dependencyVerifications {
    checksums batmDependencyChecksumsConfig
    printUnusedAssertions false
}