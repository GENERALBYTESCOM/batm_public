apply plugin: "java"
apply plugin: "com.generalbytes.gradle.main"
apply plugin: 'maven-publish'

group = projectGroup
version = projectVersion

jar {
    baseName 'batm_server_extensions_extra'
    archiveName "${baseName}.${extension}"
}

configurations {
    artifactOnly
    runtimeNoArtifact {
        extendsFrom compile
    }

    // handle Github (e.g. community) vs GENERAL BYTES dichotomy
    generalBytesCompile
    githubCompile
    if (hasGbArtifactory) {
        compile { extendsFrom generalBytesCompile }
    } else {
        compile { extendsFrom githubCompile }

    }
}

artifacts {
    artifactOnly jar
}

ext {
    xchangeVersion = '5.0.10'
}

dependencies {
    compile project(':server_extensions_api')
    compile project(':currencies')
    compile(group: 'com.github.Polve', name: 'JavaBitcoindRpcClient', version: javaBitcoindRpcVersion)
    compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.28')
    compile(group: 'com.github.mmazi', name: 'rescu', version: '2.0.2')
    compile(group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1')
    compile(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.9')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.9')
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9')
    compile(group: 'com.google.guava', name: 'guava', version: '27.0.1-android')
    compile(group: 'com.google.zxing', name: 'core', version: '2.3.0')
    compile(group: 'org.knowm.xchange', name: 'xchange-core', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-bitfinex', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-binance', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-bittrex', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-hitbtc', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-dvchain', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-coinbasepro', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-coingi', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-enigma', version: xchangeVersion)
    compile(group: 'org.knowm.xchange', name: 'xchange-poloniex', version: xchangeVersion)
    compile(group: 'org.web3j', name: 'core', version: '3.6.0')
    compile(group: 'org.web3j', name: 'crypto', version: '3.6.0')
    compile(group: 'org.web3j', name: 'utils', version: '3.6.0')
    compile(group: 'org.web3j', name: 'abi', version: '3.6.0')
    compile(group: 'org.java-websocket', name: 'Java-WebSocket', version: '1.3.8')
    compile(group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.12.1')
    compile(group: 'com.squareup.okio', name: 'okio', version: '1.17.2')
    compile(group: 'io.reactivex', name: 'rxjava', version: '1.2.4')
    compile(group: 'com.vdurmont', name: 'emoji-java', version: '3.1.3') //for chat emojis
    compile(group: 'com.nexmo', name: 'client', version: '5.5.0')       // sms provider
    implementation group: 'org.bitcoinj', name: 'bitcoinj-core', version: '0.15.10'
    implementation group: 'com.github.dogecoin', name: 'libdohj', version: '7e68d7e19b6c28f231418d56fc2ee542e9e40ed5'

    compile(group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.63')

    runtime(group: 'com.google.protobuf', name: 'protobuf-java', version: '2.6.1')

    generalBytesCompile(group: 'com.generalbytes.bitrafael.public', name: 'bitrafael-client', version: bitrafaelVersion) {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    githubCompile(group: 'com.github.GENERALBYTESCOM.bitrafael_public', name: 'bitrafael-client', version: bitrafaelVersion) {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    generalBytesCompile(group: 'com.generalbytes.bitrafael.public', name: 'bitrafael-client-api', version: bitrafaelVersion)
    githubCompile(group: 'com.github.GENERALBYTESCOM.bitrafael_public', name: 'bitrafael-client-api', version: bitrafaelVersion)
    generalBytesCompile(group: 'com.generalbytes.bitrafael.public', name: 'bitrafael-server-api', version: bitrafaelVersion)
    githubCompile(group: 'com.github.GENERALBYTESCOM.bitrafael_public', name: 'bitrafael-server-api', version: bitrafaelVersion)

    compile(group: 'com.github.generalbytescom.trident', name:'abi', version:'fff6ed0') // fff6ed0 should be close to 0.1.1
    compile(group: 'com.github.generalbytescom.trident', name:'core', version:'fff6ed0')
    compile(group: 'com.github.generalbytescom.trident', name:'utils', version:'fff6ed0')
    compile(group: 'com.google.protobuf', name:'protobuf-java', version: '3.11.0')

    testCompile(group: 'junit', name: 'junit', version: '4.10')
    testCompile(group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3')
    testCompile(group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3')
}

dependencySubstitutions {
    substitute file(batmDependencySubstitutionConfig)
}

publishing {
    if (hasGbUploadArtifactory) {
        repositories {
            maven {
                credentials {
                    username gbArtifactoryUser
                    password gbArtifactoryPassword
                }
                url gbArtifactoryUploadUrl
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

dependencyVerifications {
    checksums batmDependencyChecksumsConfig
    printUnusedAssertions false

    // additional configurations
    if (hasGbArtifactory) {
        configuration project.configurations.generalBytesCompile
    }
    configuration project.configurations.githubCompile
}

test {
    testLogging {
        events "failed"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}